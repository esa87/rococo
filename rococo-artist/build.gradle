plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.cloud.tools.jib' version '3.4.1'
}

group = 'guru.qa'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
	maven { url 'https://repo.spring.io/snapshot' }
}



dependencies {
	//spring
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	//db
	implementation "org.flywaydb:flyway-core:${project.ext.flywayVersion}"
	implementation "org.flywaydb:flyway-mysql:${project.ext.flywayVersion}"
	runtimeOnly "mysql:mysql-connector-java:${project.ext.mysqlDriverVersion}"
	//grpc
	implementation 'io.grpc:grpc-services'
	implementation "net.devh:grpc-spring-boot-starter:${project.ext.springGrpcVersion}"
	// Protobuf
	implementation "com.google.protobuf:protobuf-java:${project.ext.protobufVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${project.ext.protobufVersion}"
	implementation project(':rococo-grpc-common')
	//lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	//test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

jib {
	container {
		ports = ['8091']
		jvmFlags = ["-Dspring.profiles.active=${System.env.PROFILE}"]
		environment = [
				'TZ'         : 'Europe/Moscow'
		]
		creationTime = 'USE_CURRENT_TIMESTAMP'
		labels = [
				'maintainer': 'Erchenkov Sergey @esa_87',
				'version'   : "${project.version}".toString()
		]
	}
	from {
		image = "${project.ext.dockerImage}"
		platforms {
			platform {
				architecture = "${project.ext.dockerArch}"
				os = 'linux'
			}
		}
	}
	to {
		image = "${project.ext.dockerHubName}/${project.name}-${System.env.PROFILE}"
		tags = ['latest', "${project.version}"]
	}
}

tasks.jib.dependsOn test
tasks.jibDockerBuild.dependsOn test
